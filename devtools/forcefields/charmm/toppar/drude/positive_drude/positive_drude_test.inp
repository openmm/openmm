* test ions with swm4 water
*

set toppardir .

stream @toppardir/toppar_drude_positive_swm4_ions.str

!data file
open unit 33 write form name swm4_ion.dat

!test water model alone

!! Generate water residues 

read sequence SWM4 5
generate BULK setup noangle nodihedral drude dmass 0.4

read coor card
* water coordinated
*
   15
    1    1 SWM4 OH2    7.38263   0.89233  -4.71643 BULK 1     -2.47655
    2    1 SWM4 H1     7.30586   0.03258  -4.30272 BULK 1     -2.60884
    3    1 SWM4 H2     7.54089   1.49503  -3.98984 BULK 1     -3.13461
    4    2 SWM4 OH2   -6.20797   4.45953   0.73205 BULK 2      0.94227
    5    2 SWM4 H1    -5.29193   4.71233   0.84693 BULK 2      1.64723
    6    2 SWM4 H2    -6.68689   5.00122   1.35929 BULK 2      0.17114
    7    3 SWM4 OH2    6.06969   6.29872   6.92235 BULK 3      2.53526
    8    3 SWM4 H1     6.72984   6.67673   7.50333 BULK 3      3.25850
    9    3 SWM4 H2     5.35053   6.05270   7.50417 BULK 3      2.71730
   10    4 SWM4 OH2    0.22109  -4.53529  -5.63924 BULK 4     -0.75622
   11    4 SWM4 H1     0.41942  -4.34638  -6.55642 BULK 4     -0.30566
   12    4 SWM4 H2    -0.71907  -4.71479  -5.62906 BULK 4     -0.12645
   13    5 SWM4 OH2   -5.19509   1.26039  -1.16892 BULK 5      1.07017
   14    5 SWM4 H1    -5.93523   1.34878  -0.56841 BULK 5      0.70060
   15    5 SWM4 H2    -5.53474   1.55603  -2.01359 BULK 5      1.11651

!=========================================================
! DRUDE and LONE PAIR SETUP
!---------------------------------------------------------
coor sdrude
coor shake

SHAKE bonh param tolerance 1.0e-9 -
      nofast -
      select ( .not. type D* ) end -
      select ( .not. type D* ) end

coor print

update inbfrq -1 ihbfrq 0 atom switch vatom vfswitch cutnb 99.0 ctofnb 89.0 ctonnb 79.0

! Minimize positions of Drude particles
! constrain position of regular atoms
!cons fix select .not. type D* end
energy
set einit ?ener
mini SD nstep 10 nprint 1
cons fix select none end
energy

write title unit 33
* SWM4-NDP pentamer
* Einitial = @einit
* Umin = ?ENER
*

drude reset
delete atom sele all end

!!  Monomer + ion

set count 1

label ion_loop

if count eq 1 set iontype LIT
if count eq 2 set iontype SOD
if count eq 3 set iontype POT
if count eq 4 set iontype RUB
if count eq 5 set iontype CES
if count eq 6 set iontype FLU
if count eq 7 set iontype CLA
if count eq 8 set iontype BRO
if count eq 9 set iontype IOD

read sequence SWM4 1
generate SOLV setup noangle nodihedral drude dmass 0.4
read sequence @IONTYPE 1
generate ION setup first none last none drude dmass 0.4

read coor card
* SWM4
*
    4
    1    1 SWM4 OH2    1.25163  -0.00023   0.27019
    2    1 SWM4 OM     1.37619  -0.00008   0.06798
    3    1 SWM4 H1     1.55899   0.75708  -0.22804
    4    1 SWM4 H2     1.55883  -0.75682  -0.22925

coor orient select type O* end
coor set xdir -3.0 select type @IONTYPE end
coor init sele type D* end

coor print
coor sdrude
coor print
print psf
bomlev -1
coor shake
coor print
coor stat

update inbfrq -1 ihbfrq 0 atom switch vatom vfswitch cutnb 99.0 ctofnb 89.0 ctonnb 79.0

!==================================================
! Water-ion interaction
!--------------------------------------------------

energy
set einit ?ener
mini SD step 0.01 nstep 5000 nprint 1000
coor orient select type O* .or. type @IONTYPE end

energy                             ! ?ENER
quick 1 6                          ! ?DIST
coor dipole select segid SOLV end  ! ?RDIP

write title unit 33
* @IONTYPE
* Einitial = @einit
* Umin = ?ENER
* Dmin = ?DIST
* mu   = ?RDIP
*

drude reset
delete atom select all end

calc count = @count + 1

if count le 9 goto ion_loop

stop
