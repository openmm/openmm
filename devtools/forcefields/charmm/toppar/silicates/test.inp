* Generation of crystal slab followed by minimization
*             Pedro Lopes, Jul 2005
*

! The surface is generated by creating the appropriate
! images of the central unitcell using the IMAGE
! facility of charmm. The user starts from a pdb file
! of the primitive cell, with the surface on the XY 
! plane. This script assumes this file is file.pdb.
! From the primitive cell a surface is created by 
! replicating it 8x5 times. The crystal parameters and 
! number of replicas are defined in params.dat
! Other files needed to run this script are:
!    crystal.img and image_patch.str


bomlev -2      

! read the topology and parameter file

open read formatted unit 12 name toppar/top_silicates.inp  
read rtf card unit 12
close unit 12
open read formatted unit 12 name toppar/par_silicates.inp  
read para card unit 12
close unit 12

! Generate "unit cell" psf and coordinates

! Generate the molecule, corresponing to the "unit cell"
read sequ card
* quartz
*
1
Q011

generate q1 noangle nodihedral first none last none setup warn

open unit 30 read form name quartz_init.pdb
read coor pdb unit 30
close unit 30

AUTOgenerate ANGLES DIHEdrals
   
!PRINT COOR
!PRINT PSF

!======================================================
!               Set up periodic images
!======================================================

stream params.str

!initialise number of replicas along U and V
set maxU @n 
set maxV @m

!initialise unitcell counters and total cell count    
set countU 2                    
set countV 1
set count  1

!start loops

label loopV

   label loopU

! generate copy of unit cell. name it Q@COUNT

      calc vectU = @countU - 1.0
      calc vectV = @countV - 1.0

      calc count = @count + 1

      read sequ card
      * quartz
      *
      1
      Q011

      generate Q@count noangle nodihedral first none last none setup warn

      coor dupl sele segi q1       end sele segi q@count end
 
      coor trans XDir @vectU   YDIR 0.0 ZDIR 0.0 FACT @u1 SELE SEGI q@count END
      coor trans XDir 0.0 YDIR @vectU   ZDIR 0.0 FACT @u2 SELE SEGI q@count END
      coor trans XDir 0.0 YDIR 0.0 ZDIR @vectU   FACT @u3 SELE SEGI q@count END

      coor trans XDir @vectV   YDIR 0.0 ZDIR 0.0 FACT @v1 SELE SEGI q@count END
      coor trans XDir 0.0 YDIR @vectV   ZDIR 0.0 FACT @v2 SELE SEGI q@count END
      coor trans XDir 0.0 YDIR 0.0 ZDIR @vectV   FACT @v3 SELE SEGI q@count END

      AUTOgenerate ANGLES DIHEdrals  

      calc countU = @countU + 1      
  
!     PRINT COOR
!     PRINT PSF

   if countU le @maxU goto loopU
  
   set countU 1

   calc countV = @countV + 1

if countV le @maxV goto loopV

!PRINT COOR
!PRINT PSF

! apply patches

set count 0

calc endXloop = @maxU - 1.0
calc endYloop = @maxV - 1.0

set incX 1
set incY 1

label  loopY
   
   calc count = @count + 1.0
   
   label loopX

      calc X = @count + 1.0

      patch 011A q@count 1 q@x 1 setup warn   ! patch along U

      AUTOgenerate ANGLES DIHEdrals
  
      calc Y = @count + @maxU       

      patch 011B q@count 1 q@y 1 setup warn   ! patch along V

      AUTOgenerate ANGLES DIHEdrals

! patch

      calc XY = @X + @maxU 

      patch 011C q@count 1 q@xy 1 setup warn   ! patch along UV

      AUTOgenerate ANGLES DIHEdrals

! patch

      calc count = @count + 1.0

      calc incX = @incX + 1.0

   if incX le @endXloop goto loopX

   set incX 1

   calc incY = @incY + 1.0

   calc lastX = @X + @maxU 
 
   patch 011B q@X 1 q@lastX 1 setup warn   ! patch last column along V

   AUTOgenerate ANGLES DIHEdrals

if incY le @endYloop goto loopY

set incXend 1

calc count = @count + 1.0

label loopXend

   calc Xend = @count + 1.0

   patch 011A q@count 1 q@Xend 1 setup warn   ! patch along U

   AUTOgenerate ANGLES DIHEdrals

   calc incXend = @incXend + 1.0

   calc count = @count + 1.0

if incXend le @endXloop goto loopXend

!PRINT COOR
!PRINT PSF

! Minimize initial structure. 
! Only hydrogens are allowed to move.
cons harm force 10000.0 mass select .not. type H* end
mini sd nsteps 500 nprint 50

cons harm force 0.0 mass select all end


!========================================================
!  Generation of crystal slab followed by minimization 
!  The surface is generated by creating the appropriate 
!  images of the central unitcell using the IMAGE 
!  facility of charmm
!========================================================


! 3: cutim
! 4: cutnb
! 5: ctonnb
! 6: ctofnb
set 3 16.0 ! cutim
set 4 16.0 ! cutnb
set 5 10.0 ! ctonnb
set 6 12.0 ! ctofnb
                                                                                                                                                             
! Create images now
open unit 8 read card name crystal.img
read imag print init unit 8
close unit 8

! Manipulation of images
update inbf 0 ihbf 0 imgfrq 100 cutim @3
stream image_patch.str

!PRINT PSF
!PRINT IMAG PSF

update inbfrq -1 imgfrq -1 ihbfrq 0 -
nbonds  atom  fshift cdie  vdw vshift  -
        cutnb @4    ctofnb @6   ctonnb @5   wmin 1.5  eps 1.0

mini sd nsteps 500 nprint 50
energy

open write card unit 20 name test_opt.pdb
write coor pdb unit 20
*  COORdinates
*
close unit 20
                                                                                                                                                             
open write form unit 20 name test_opt.crd
write coor cards unit 20
*  COORdinates
*
close unit 20

open unit 20 write form name test_opt.psf
write psf cards unit 20
* PSFfile
*
close unit 20

stop

